@tool
class_name BlurGeneratorCompositor
extends CompositorEffect

# When the color texture has generated, this signal will be emitted
signal texture_generated(color_texture: Texture2DRD)

var current_accumulation: int = 1

var rd: RenderingDevice
var shader: RID
var pipeline: RID

var nearest_sampler: RID

var texture: RID
var texture_2d_rd := Texture2DRD.new()


func _init():
	effect_callback_type = CompositorEffect.EFFECT_CALLBACK_TYPE_POST_TRANSPARENT
	
	rd = RenderingServer.get_rendering_device()
	
	var sampler_state := RDSamplerState.new()
	
	sampler_state.min_filter = RenderingDevice.SAMPLER_FILTER_NEAREST
	sampler_state.mag_filter = RenderingDevice.SAMPLER_FILTER_NEAREST
	sampler_state.repeat_u = RenderingDevice.SAMPLER_REPEAT_MODE_CLAMP_TO_EDGE
	sampler_state.repeat_v = RenderingDevice.SAMPLER_REPEAT_MODE_CLAMP_TO_EDGE
	
	nearest_sampler = rd.sampler_create(sampler_state)
	
	RenderingServer.call_on_render_thread(_initialize_compute)


func _notification(what: int) -> void:
	if what == NOTIFICATION_PREDELETE:
		if shader.is_valid():
			# Pipeline is a dependent that will get deleted automatically
			rd.free_rid(shader)
		
		if texture.is_valid():
			rd.free_rid(texture)
		
		if nearest_sampler.is_valid():
			rd.free_rid(nearest_sampler)


func _initialize_compute() -> void:
	rd = RenderingServer.get_rendering_device()
	
	if not rd:
		return
	
	var shader_file: RDShaderFile = load("res://blur_generator.glsl")
	var shader_spirv: RDShaderSPIRV = shader_file.get_spirv()
	
	shader = rd.shader_create_from_spirv(shader_spirv)
	
	if shader.is_valid():
		pipeline = rd.compute_pipeline_create(shader)


func _render_callback(p_effect_callback_type: EffectCallbackType, p_render_data: RenderData) -> void:
	if !rd and !pipeline.is_valid():
		return
	
	var render_scene_buffers: RenderSceneBuffersRD = p_render_data.get_render_scene_buffers()
	
	if !render_scene_buffers:
		return
	
	# Get our render size, this is the 3D render resolution!
	var size: Vector2i = render_scene_buffers.get_internal_size()
	
	if size.x == 0 and size.y == 0:
		return
	
	if not texture.is_valid() or Vector2i(texture_2d_rd.get_size()) != size:
		_build_texture(size.x, size.y)
	
	# Define invocation group size
	@warning_ignore("integer_division")
	var x_groups: int = (size.x - 1) / 16 + 1
	
	@warning_ignore("integer_division")
	var y_groups: int = (size.y - 1) / 16 + 1
	
	var z_groups: int = 1
	
	var color_sampler_uniform: RDUniform = RDUniform.new()
	color_sampler_uniform.uniform_type = RenderingDevice.UNIFORM_TYPE_SAMPLER_WITH_TEXTURE
	color_sampler_uniform.binding = 0
	color_sampler_uniform.add_id(nearest_sampler)
	color_sampler_uniform.add_id(render_scene_buffers.get_color_layer(0))
	
	var texture_uniform: RDUniform = RDUniform.new()
	texture_uniform.uniform_type = RenderingDevice.UNIFORM_TYPE_IMAGE
	texture_uniform.binding = 1
	texture_uniform.add_id(texture)
	
	var uniform_set: RID = UniformSetCacheRD.get_cache(shader, 0, [color_sampler_uniform, texture_uniform])
	
	var compute_list: int = rd.compute_list_begin()
	
	var push_constants: PackedInt32Array = [
		current_accumulation,
		0,
		0,
		0
	]
	
	# NOTE @sphynx-owner: imporatnt that the accumulation
	# increments after it's fed to the push constants, otherwise
	# it would never start at 1.
	current_accumulation += 1
	
	var byte_push_constants: PackedByteArray = push_constants.to_byte_array()
	
	rd.compute_list_bind_compute_pipeline(compute_list, pipeline)
	rd.compute_list_bind_uniform_set(compute_list, uniform_set, 0)
	rd.compute_list_set_push_constant(compute_list, byte_push_constants, byte_push_constants.size())
	rd.compute_list_dispatch(compute_list, x_groups, y_groups, z_groups)
	rd.compute_list_end()


# fetched from https://github.com/dmlary/godot-demo-sencil-buffer-compositor-effect
## Create a new color texture to use as the output for our render pipeline.
## Note: this texture must be the same size as the color texture, so we create
## it on demand.
func _build_texture(width: int, height: int):
	print("compositor building texture")
	
	var texture_format := RDTextureFormat.new()
	
	# create our output texture
	texture_format = RDTextureFormat.new()
	texture_format.texture_type = RenderingDevice.TEXTURE_TYPE_2D
	texture_format.width = width
	texture_format.height = height
	texture_format.format = RenderingDevice.DATA_FORMAT_R32G32B32A32_SFLOAT
	texture_format.usage_bits = RenderingDevice.TEXTURE_USAGE_SAMPLING_BIT | RenderingDevice.TEXTURE_USAGE_STORAGE_BIT | RenderingDevice.TEXTURE_USAGE_CAN_COPY_FROM_BIT
	
	var new_texture = rd.texture_create(texture_format, RDTextureView.new())
	
	assert(new_texture.is_valid())
	
	var old_texture: RID = texture
	
	texture = RID()
	
	# save the new texture rid
	texture = new_texture
	
	# HACK: We wait with both releasing the old texture, and replacing the
	# texture_rd_rid, since it seems to clash with godot's rendering pipeline otherwise
	await RenderingServer.frame_post_draw
	
	texture_2d_rd.texture_rd_rid = texture
	
	# free the old texture if there was one
	if old_texture.is_valid():
		rd.free_rid(old_texture)
	
	texture_generated.emit(texture_2d_rd)
